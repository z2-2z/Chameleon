struct Char {
    _: char = 0x20..0x21, 0x23..0x5B, 0x5D..0x7F;
}

struct HexDigit {
    _: char = 0x30..0x39, 0x41..0x46, 0x61..0x66;
}

struct String {
    _: char = '"';
    repeats 0..16 chars: oneof {
        char: Char;
        esc: struct {
            _: char = '\\';
            escc: oneof {
                _: char = '\\', '/', 'b', 'f', 'n', 'r', 't', '"';
                u: struct {
                    _: char = 'u';
                    repeats 4 digit: HexDigit;
                };
            };
        };
    };
    _: char = '"';
}

struct Member {
    key: String;
    _: char = ':';
    value: Value;
}

struct Object {
    _: char = '{';
    optional members: struct {
        repeats 0..8 _: struct {
            member: Member;
            comma: char = ',';
        };
        member: Member;
    };
    _: char = '}';
}

struct Array {
    _: char = '[';
    optional elements: struct {
        repeats 0..8 _: struct {
            element: Value;
            comma: char = ',';
        };
        element: Value;
    };
    _: char = ']';
}

struct Digits {
    char: oneof {
        zero: char = '0';
        digits: struct {
            lead: char = 0x31..0x39;
            repeats 0..19 follow: char = 0x30..0x39;
        };
    };
}

struct Number {
    optional sign: char = '-';
    digits: Digits;
    optional frac: struct {
        _: char = '.';
        repeats 1..20 digits: char = 0x30..0x39;
    };
    optional exp: struct {
        e: char = 'e', 'E';
        sign: char = '+', '-';
        repeats 1..20 digits: char = 0x30..0x39;
    };
}

struct Value {
    value: oneof {
        option scheduling = random;
    
        object: Object;
        array: Array;
        string: String;
        number: Number;
        literal: oneof {
            true: string = "true";
            false: string = "false";
            null: string = "null";
        };
    };
}

struct Root {
    value: Value;
}
